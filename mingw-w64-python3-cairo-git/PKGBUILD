# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pycairo

pkgname="${MINGW_PACKAGE_PREFIX}-python3-cairo"
pkgver=1.10.1.34.5a45b0b
pkgrel=1
pkgdesc="Python 3 bindings for the cairo graphics library (mingw-w64)"
url="http://www.cairographics.org/pycairo"
arch=('any')
license=('LGPL' 'MPL')
depends=("${MINGW_PACKAGE_PREFIX}-python3" "${MINGW_PACKAGE_PREFIX}-cairo")

source=("git://git.cairographics.org/git/pycairo"
  "0001-msys2-port-to-subprocess.check_output.patch"
  "0002-add-C-msys-prefix-to-fix-build-won-t-work-on-differe.patch"
  "0003-fix-PyVarObject_HEAD_INIT-problem.patch")
md5sums=('SKIP'
  "c204e6d6d712d257c7791eef018530bd"
  "6e5ccbadcd22f9fe2a2b31493c805b27"
  "ca7726ee837d49de8130db7efdc7d3c0")

pkgver() {
  cd $_realname
  PREFIX="pycairo_version        = '"
  ver=$(grep "$PREFIX" setup.py | sed "s/$PREFIX//" | sed "s/'//")
  revision=$(git rev-list --count HEAD)
  hash=$(git log --pretty=format:'%h' -n 1)
  echo $ver.$revision.$hash
}

prepare() {
  cd $_realname
  git am ../0001-msys2-port-to-subprocess.check_output.patch
  git am ../0002-add-C-msys-prefix-to-fix-build-won-t-work-on-differe.patch
  git am ../0003-fix-PyVarObject_HEAD_INIT-problem.patch
}

package() {
  cd $_realname
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root=$pkgdir/
  mv $pkgdir/msys/mingw64 $pkgdir/mingw64
  rm $pkgdir/msys
}
