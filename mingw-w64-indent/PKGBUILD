# $Id$
# Maintainer: 

_name=indent
pkgname="${MINGW_PACKAGE_PREFIX}-${_name}"
pkgver=2.2.11
pkgrel=3
pkgdesc="C language source code formatting program (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/indent/"
license=('GPL3')
#depends=("${MINGW_PACKAGE_PREFIX}-glibc")
makedepends=("${MINGW_PACKAGE_PREFIX}-texi2html")
install=indent.install
source=(http://ftp.de.debian.org/debian/pool/main/i/indent/${_name}_${pkgver}.orig.tar.gz
        indent-2.2.11-segfault.patch
        indent-2.2.11-LC_ALL.patch
        indent-2.2.11-decimal_format.patch)
sha1sums=('9b242528bbc4914464117c69ffae9df24d3fa4a7'
          '31a6dcd5edcac4decac81dbb52bc9415b8b148e4'
          '91b210b666de59ca44524288a4c1ebd213570daa'
          '2b7d5a725fe307ce81565f920a0a66b319b50c91')

prepare() {
  cd "${srcdir}/${_name}-${pkgver}"
  patch -Np1 -i ../indent-2.2.11-segfault.patch

  # http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=205692
  patch -Np1 -i ../indent-2.2.11-LC_ALL.patch

  # https://lists.gnu.org/archive/html/bug-indent/2011-08/msg00000.html
  patch -Np1 -i ../indent-2.2.11-decimal_format.patch
   
  sed -i 's/-number/-number-sections/' doc/Makefile.in
}

build() {
  cd "${srcdir}/${_name}-${pkgver}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules
  make
}

check() {
  cd "${srcdir}/${_name}-${pkgver}/regression"
  make test
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  make DESTDIR="${pkgdir}" docdir=/usr/share/doc/indent install
}
